from os import listdir
from os.path import isfile, splitext

rule all:
    input:
        "mapping/stats/mapping_stats.xlsx",
        expand("mapping/{A}.bam", A=sorted(config['entries'].keys())),
        ".report/modules/bowtie2.html"


rule mapping_stats_xlsx:
    input:
        "mapping/stats/mapping_stats.tsv"
    output:
        xlsx="mapping/stats/mapping_stats.xlsx",
        plot_alignment_absolute="mapping/stats/alignment_stats.svg",
        plot_alignment_relative="mapping/stats/alignment_stats_relative.svg"
    params:
        plot_dir="mapping/stats/"
    conda:
        "../lib/conda_env.yaml"
    group:
        "bowtie2_statistics"
    shell:
        "python3 lib/pe_mapping_stats_tsv_to_xlsx.py {input} {output.xlsx} {params.plot_dir}"


rule mapping_stats_tsv:
    input:
        expand("mapping/logs/bowtie2_mapping.{name}.log", name=config['entries'].keys())
    output:
        "mapping/stats/mapping_stats.tsv"
    group:
        "bowtie2_statistics"
    shell:
        """
        echo "sample	reads	reads[%]	aligned_conc_0_times	aligned_conc_0_times[%]	aligned_conc_1_time	aligned_conc_1_time[%]	aligned_conc_more_than_1_times	aligned_conc_more_than_1_times[%]	aligned_disconc_1_time	aligned_disconc_1_time[%]	pairs_aligned_0_times_conc_or_disconc	in_mates	mates_aligned_0_times	mates_aligned_0_times[%]	mates_aligned_1_time	mates_aligned_1_time[%]	mates_aligned_more_than_1_times	mates_aligned_more_than_1_times[%]	overall_alignment_rate" > {output}
        for i in {input}; do
            sample=$(basename $i);
            sample=${{sample%*.log}};
            sample=${{sample#bowtie2_mapping.}};
            echo -n $sample"	" >> {output};
            tail -n+2 $i | \
            sed -e '6d' -e 's/^[^0-9]*\([0-9]\{{1,\}}\) (\([0-9.]\{{1,\}}\)%).*/\\1\t\\2/' \
            -e 's/^[^0-9]*\([0-9]\{{1,\}}\) pairs.*/\\1/' \
            -e 's/^\(.*\)%.*$/\\1/' -e 's/^[^0-9]*\([0-9]\{{1,\}}\) mates.*/\\1/' -e 's/^.*----.*$//' | \
            tr '\n' '\t' | \
            sed -e 's/\t$//' | \
            sed -e 's/\t\t/\t/g' >> {output}
            echo >> {output};
        done;
        """


rule bowtie2_index:
    params:
        prefix=splitext("%%GENOME_FASTA%%")[0]
    input:
        genome="%%GENOME_FASTA%%"
    output:
        splitext("%%GENOME_FASTA%%")[0] + ".1.bt2"
    conda:
        "../lib/conda_env.yaml"
    group:
        "bowtie2_index"
    log:
        "mapping/logs/bowtie2_index.log"
    threads:
        8
    shell:
        "bowtie2-build --threads {threads} {input.genome} {params.prefix} 2>&1 |"
        "tee {log}"


rule bowtie2_mapping:
    params:
        prefix=splitext("%%GENOME_FASTA%%")[0]
    input:
        genome="%%GENOME_FASTA%%",
	    genome_index=splitext("%%GENOME_FASTA%%")[0] + ".1.bt2",
	    reads="preprocessing/{sample}_R1.fastq",
	    reads_reverse="preprocessing/{sample}_R2.fastq"
    output:
        temp("mapping/sam/{sample}.sam")
    conda:
        "../lib/conda_env.yaml"
    group:
        "bowtie2_mapping"
    log:
        "mapping/logs/bowtie2_mapping.{sample}.log"
    threads:
        2
    shell:
        "bowtie2 %%ADDITIONAL_BOWTIE2_OPTIONS%% -p {threads} --mm --very-sensitive-local -x {params.prefix} -1 {input.reads} -2 {input.reads_reverse} -S {output} 2>&1 |"
        "tee {log}"


rule index_bam:
    input:
        "mapping/sam/{sample}.sam"
    output:
        bam="mapping/{sample}.bam",
        bai="mapping/{sample}.bam.bai",
        bam_unmapped="mapping/unmapped/{sample}_unmapped.bam",
        bam_singleton="mapping/singleton/{sample}_singletons.bam",
        bam_disconc="mapping/disconcordantly/{sample}_disconc.bam"
    params:
        tmp_singleton=temp("mapping/singleton/{sample}_tmp.sam")
    conda:
        "../lib/conda_env.yaml"
    group:
        "bowtie2_mapping"
    threads:
        2
    shell:
        "samtools view -F 12 -f 2 -Shb {input} | samtools sort -@ {threads} -o {output.bam} - && samtools index {output.bam};"

        "samtools view -f 4 -F 8 -Sh {input} > {params.tmp_singleton} && samtools view -f 8 -F 4 -S {input} >> {params.tmp_singleton} "
        "&& samtools view -Shb {params.tmp_singleton} | samtools sort -@ {threads} -o {output.bam_singleton} - && rm {params.tmp_singleton};"

        "samtools view -F 14 -Shb {input} | samtools sort -@ {threads} -o {output.bam_disconc} -;"

        "samtools view -f 12 -Shb {input} | samtools sort -@ {threads} -o {output.bam_unmapped} -;"


rule generate_report_data:
    input:
        stats_tsv="mapping/stats/mapping_stats.tsv",
        images=["mapping/stats/alignment_stats.svg", "mapping/stats/alignment_stats_relative.svg"]
    output:
        bw2_data=".report/data/bowtie2_data.js",
        bw2_html=".report/modules/bowtie2.html",
        bw2_js=".report/js/modules/bowtie2.js",
        bw2_css=".report/css/modules/bowtie2.css",
        bw2_images=directory(".report/img/modules/bowtie2/")
    conda:
        "../lib/conda_env.yaml"
    group:
        "bowtie2_report"
    shell:
        "python3 lib/generate_report_data.py --stats {input.stats_tsv} --output {output.bw2_data} --paired-end && "
        "cp lib/report/bowtie2.html {output.bw2_html} && "
        "cp lib/report/bowtie2.js {output.bw2_js} && "
        "cp lib/report/bowtie2.css {output.bw2_css} &&"
        "cp {input.images} {output.bw2_images}"
